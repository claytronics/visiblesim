
include #heat-data.meld

//type inbound(node, int).
//type route edge(node, node, float).
//type coord(node, int, int).
//type inner(node).
//type linear heat(node, float).
type linear heatchanged(node, float, float, int).
type linear neighborchanged(node, node, float, float).
type linear delta(node, float, float, int).

priority @order @desc.
priority @cluster @static.
priority @initial 100.

const epsilon = str2float(@arg1).
const delta = str2float(@arg2).

fun fabs(float X) : float = if X >= 0.0 then 1 * X else -1.0 * X end.

heatchanged(A, 0.0, 0.0, 0).
delta(A, 0.0, 0.0, 0).

/* neighbor heat has changed... we add this to delta */
neighborchanged(A, B, V, O),
delta(A, P1, P2, C),
!edge(A, B, W)
   -o delta(A, P1 + V * W, P2 + O * W, C + 1).

/* we changed our heat, send this information to neighbors */
heatchanged(A, O, X, N),
heat(A, V),
fabs(X) > delta,
!inbound(A, T)
   -o heat(A, V),
      {B, W | !edge(A, B, W) | add-priority(B, fabs(X) / delta), neighborchanged(B, A, V / float(T), O / float(T))}.

/* ... same thing, except delta was not big enough to change priority */
heatchanged(A, O, X, N),
heat(A, V),
!inbound(A, T)
   -o heat(A, V),
      {B, W | !edge(A, B, W) | neighborchanged(B, A, V / float(T), O / float(T))}.

delta(A, Plus, Minus, N),
heat(A, V),
!inbound(A, X),
fabs(Plus - Minus) > epsilon
   -o heatchanged(A, V, Plus - Minus, N), heat(A, V + Plus - Minus),
      delta(A, 0.0, 0.0, 0).

